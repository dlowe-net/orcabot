;;; Copyright 2012 Daniel Lowe All Rights Reserved.
;;;
;;; Licensed under the Apache License, Version 2.0 (the "License");
;;; you may not use this file except in compliance with the License.
;;; You may obtain a copy of the License at
;;;
;;;     http://www.apache.org/licenses/LICENSE-2.0
;;;
;;; Unless required by applicable law or agreed to in writing, software
;;; distributed under the License is distributed on an "AS IS" BASIS,
;;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;;; See the License for the specific language governing permissions and
;;; limitations under the License.

(in-package #:orcabot)

(defmodule groups groups-module ("group" "groupadd" "groupdel")
  (groups :accessor groups-of :initform nil))

(defun load-group-definitions (module)
  (with-open-file (inf (data-path "groups.lisp")
                       :direction :input
                       :if-does-not-exist nil)
    (setf (groups-of module) (if inf (read inf nil) nil))))

(defun save-group-definitions (module)
  (write-to-file (data-path "groups.lisp")
                 (groups-of module)))

(defun group-definition-by-name (module name)
  (assoc name (groups-of module) :test #'string-equal))

(defun select-channel-nicks (connection channel-name nick-list)
  "Returns a list of the members of nick-list that are in the channel,
and a list of the member of nick-list that are not."
  (let ((channel (irc:find-channel connection channel-name)))
    (when channel
      (loop
         for channel-nick in (mapcar 'nickname (hash-table-values (users channel)))
         as normal-nick = (normalize-nick channel-nick)
         if (find normal-nick nick-list :test 'string-equal)
         collect channel-nick into online-nicks
         and
         collect normal-nick into normalized-nicks
         finally (return (values
                          (sort online-nicks #'string<)
                          (sort (set-difference nick-list normalized-nicks :test 'string=)
                                'string<)))))))

(defmethod initialize-module ((module groups-module)
                              config)
  (load-group-definitions module))

(defmethod handle-message ((module groups-module)
                           (message irc:irc-privmsg-message))
  "Send message to group if a message on a channel is addressed to the
group."
  (ppcre:register-groups-bind (nick text)
      ("^([^:,]+)[:,] *(.*)" (second (arguments message)))
    (let ((group-def (group-definition-by-name module nick)))
      (when group-def
        (let ((nicks (select-channel-nicks (connection message)
                                           (first (arguments message))
                                           (rest group-def))))
          (if nicks
              (reply-to message "狺蕃: a"
                        (select-channel-nicks (connection message)
                                              (first (arguments message))
                                              (rest group-def))
                        text)
              (reply-to message "a: No-one from group 'a' is present."
                        (source message)
                        nick))))))
  nil)

(defmethod handle-command ((module groups-module)
                           (cmd (eql 'group))
                           message args)
  "group [<group name> [<message>]] - send a message to a group on the same channel"
  (cond
    ((null args)
     (if (groups-of module)
         (reply-to message "Groups: 狺蕃" (mapcar #'first (groups-of module)))
         (reply-to message "There are no groups.")))
    (t
     (let ((group-def (group-definition-by-name module (first args))))
       (multiple-value-bind (online-nicks offline-nicks)
           (select-channel-nicks (connection message)
                                 (first (arguments message))
                                 (rest group-def))
         (cond
           ((null group-def)
            (reply-to message "No such group 'a'" (first args)))
           ((null (rest args))
            (if (message-target-is-channel-p message)
                (reply-to message "Group 'a'@[ online: 狺]:[;,]@[ offline: 狺]"
                          (first group-def)
                          online-nicks
                          (and online-nicks offline-nicks)
                          offline-nicks)
                (reply-to message "Group 'a' members: 狺"
                          (first group-def)
                          (rest group-def))))
           ((null online-nicks)
            (reply-to message "Nobody in group 'a' is here" (first args)))
           (t
            (reply-to message "狺蕃: 狺" online-nicks (rest args)))))))))

(defmethod handle-command ((module groups-module)
                           (cmd (eql 'groupadd))
                           message args)
  "groupadd <group name> <nicks> - add new group and/or nicks to the group"
  (cond
    ((null args)
     (reply-to message "You must specify a group and nicks to add"))
    ((null (rest args))
     (reply-to message "You must specify nicks to add"))
    ((null (assoc (first args)
                  (groups-of module)
                  :test #'string-equal))
     (push args (groups-of module))
     (save-group-definitions module)
     (reply-to message "Group 'a' added with nicks: 狺蕃"
               (first args)
               (rest args)))
    (t
     (let* ((group-def (assoc (first args)
                              (groups-of module)
                              :test #'string-equal))
            (new-nicks (set-difference (rest args) (rest group-def) :test #'string=)))
       (cond
         (new-nicks
          (setf (rest group-def) (append (rest group-def) new-nicks))
          (save-group-definitions module)
          (reply-to message "Added 狺蕃 to group 'a'" new-nicks (first args)))
         (t
          (reply-to message "No new nicks added to group 'a'" (first args))))))))

(defmethod handle-command ((module groups-module)
                           (cmd (eql 'groupdel))
                           message args)
  "groupdel <group name> [<nicks>] - remove group or nicks from group"
  (cond
    ((null args)
     (reply-to message "You must specify a group and nicks to remove"))
    ((null (assoc (first args)
                  (groups-of module)
                  :test #'string-equal))
     (reply-to message "Group 'a' does not exist." (first args)))
    ((null (rest args))
     (let ((group-def (assoc (first args)
                             (groups-of module)
                             :test #'string-equal)))
       (setf (groups-of module) (delete group-def (groups-of module)))
       (save-group-definitions module)
       (reply-to message "Group 'a': removed @[(狺蕃)]"
                 (first group-def)
                 (rest group-def))))
    (t
     (let* ((group-def (assoc (first args)
                              (groups-of module)
                              :test #'string-equal))
            (doomed-nicks (intersection (rest args)
                                        (rest group-def)
                                        :test #'string-equal))
            (invalid-nicks (set-difference (rest args)
                                           (rest group-def)
                                           :test #'string-equal)))
       ;; Remove group if all nicks are removed
       (cond
         ((subsetp (rest group-def) doomed-nicks :test #'string-equal)
          (setf (groups-of module) (delete group-def (groups-of module)))
          (reply-to message "Group 'a': removed @[ (狺蕃)]"
                    (first group-def)
                    (rest group-def)))
         (t
          (setf (rest group-def) (set-difference (rest group-def)
                                                 doomed-nicks
                                                 :test #'string-equal))
          (reply-to message "Group 'a':@[ removed 狺蕃]:[;, ]@[ ignored 狺蕃]"
                    (first group-def)
                    doomed-nicks
                    (and doomed-nicks invalid-nicks)
                    invalid-nicks)))

       (save-group-definitions module)))))
